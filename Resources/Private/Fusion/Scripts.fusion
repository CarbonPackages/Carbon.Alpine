prototype(Carbon.Alpine:Scripts) < prototype(Neos.Fusion:Component) {
    files = ${Configuration.setting('Carbon.Alpine.Files')}
    main = ${Configuration.setting('Carbon.Alpine.Main')}
    useModules = ${Configuration.setting('Carbon.Alpine.useModules')}

    @private {
        attributes = '[defer]'
        type = ${props.useModules ? '(mjs)' : '(js)'}
    }

    renderer = Carbon.IncludeAssets:Collection {
        assetPackage = 'Carbon.Alpine'
        collection = Neos.Fusion:Map {
            items = ${props.files}
            itemKey = 'Folder'
            itemRenderer = Neos.Fusion:Map {
                items = ${item}
                itemKey = 'File'
                itemRenderer = ${item ? Folder + '/' + File + '.js' + private.attributes + private.type : null}
            }
            @process {
                extractSubElements = ${Array.filter(Carbon.Array.extractSubElements(value, false))}
                addMain = ${props.main ? Array.push(value, 'Main.js' + private.attributes + private.type) : value}
            }
        }
    }
}
